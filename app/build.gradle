plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}
android {
    compileSdk 34
    namespace 'com.yujing.chuankou'
    defaultConfig {
        applicationId "com.yujing.chuankou"
        minSdk 19
        targetSdk 34
        versionCode 28
        versionName "2.2.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    //签名
    signingConfigs {
        debug {
            storeFile file("doc/yujingtest.jks")
            storePassword "123456"
            keyAlias "test"
            keyPassword "123456"
        }
        release {
            storeFile file("doc/yujingtest.jks")
            storePassword "123456"
            keyAlias "test"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
            //app名称
            manifestPlaceholders = ["app_name": "@string/app_name_debug"]
            //包名后缀
            //applicationIdSuffix ".debug"
            //版本名后缀
            //versionNameSuffix "-debug"
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled false
            // 移除无用的resource文件
            shrinkResources false
            //签名配置
            signingConfig signingConfigs.debug
        }
        release {
            //app名称
            manifestPlaceholders = ["app_name": "@string/app_name"]
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled false
            //移除无用的resource文件
            shrinkResources false
            //签名配置
            signingConfig signingConfigs.release
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //打包
    android.applicationVariants.configureEach { variant ->
        //def appName = variant.productFlavors[0].name //获取渠道名称
        def appName = "YSerialPort" //rootProject.name
        def createDate = new Date().format("YYYYMMdd", TimeZone.getTimeZone("GMT+08:00"))
        def createTime = new Date().format("HHmmss", TimeZone.getTimeZone("GMT+08:00"))
        variant.outputs.configureEach {
            it.outputFileName = "${appName}_v${versionName}_${versionCode}_${createDate}_${createTime}_${buildType.name}.apk"
        }
    }
    //资源合并
    sourceSets {
        main { res.srcDirs = ['src/main/res', 'src/main/res_screen'] }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        dataBinding = true
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'//再高 minSdk 就不能是 19 了
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4' //再高 minSdk 就不能是 19 了
    implementation 'androidx.navigation:navigation-fragment-ktx:2.6.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.6.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'com.kotlinx:yutils:2.2.5'
    //bugly，异常采集
    implementation 'com.tencent.bugly:crashreport:4.1.9.3'
    //gson  https://central.sonatype.com/artifact/com.google.code.gson/gson
    //implementation 'com.google.code.gson:gson:2.11.0'
    //implementation 'com.kotlinx:yserialport:2.2.7'
    implementation project(':yserialport')
}
